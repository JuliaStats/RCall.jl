include("setup.jl")

PATH_append = String[]

# 1) Try Conda
if uppercase(get(ENV, "RCALL_CONDA", "FALSE")) == "TRUE"
    @info "Installing R via Conda.jl"
    import Conda
    Conda.add_channel("r")
    Conda.add("r")

    if Sys.iswindows()
        Rhome = joinpath(Conda.ROOTENV, "Lib", "R")
        # TODO: figure out a better way to activate the Conda environment
        # https://github.com/JuliaPy/Conda.jl/issues/119#issuecomment-508168853
        push!(PATH_append, joinpath(Conda.ROOTENV, "Library", "mingw-w64", "bin"))
    else
        Rhome = joinpath(Conda.LIBDIR, "R")
    end
    @info "Using R installation installed by Conda at $Rhome"
else
    # 2) Try R_HOME environment variable
    Rhome = get(ENV, "R_HOME", "")
    if Rhome != ""
        @info "Using R installation specified by `R_HOME` environment variable at $Rhome"
    end

    # 3) See if R is in PATH
    if Rhome == ""
        try
            global Rhome
            Rhome = readchomp(`R RHOME`)
            @info "Using R installation found in `PATH` at $Rhome"
        catch
        end
    end

    # 4) Look up Windows registry
    if Rhome == "" && Sys.iswindows()
        import WinReg
        try
            global Rhome
            Rhome = WinReg.querykey(WinReg.HKEY_LOCAL_MACHINE,
                                    "Software\\R-Core\\R", "InstallPath")
            @info "Using R installation found in Windows registry at $Rhome"
        catch
        end
    end
end
if Rhome == ""
    error("No R installation found")
end

libR = locate_libR(Rhome)
if Sys.iswindows()
    push!(PATH_append, dirname(libR))
end
@info "Using libR at $libR."

open("deps.jl", "w") do f
    println(f, "## This file is generated by deps/build.jl")
    println(f, :(const Rhome = $Rhome))
    println(f, :(const libR = $libR))
    println(f, :(const PATH_append = $PATH_append))
end
